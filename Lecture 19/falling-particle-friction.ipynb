{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5c41c817",
   "metadata": {},
   "outputs": [],
   "source": [
    "import Pkg; Pkg.activate(@__DIR__); Pkg.instantiate()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8ec79d58",
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "using Plots\n",
    "using Ipopt\n",
    "using MathOptInterface\n",
    "const MOI = MathOptInterface"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fcc1f3ad",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Parameters\n",
    "g = 9.81\n",
    "m = 1.0\n",
    "h = 0.05 #20 Hz\n",
    "Tf = 1.5 #final time (sec)\n",
    "thist = Array(0:h:Tf)\n",
    "N = length(thist)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fec57ac4",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Initial Conditions\n",
    "q0 = [0; 3.0]\n",
    "v0 = [1.0; 0.0]\n",
    "q1 = q0 + h*v0 + 0.5*h*h*[0; -g]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "41873fae",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Signed-distance function for a particle in 2D\n",
    "function ϕ(q)\n",
    "    return q[2]\n",
    "end\n",
    "\n",
    "Dϕ = [0; 1.0]'"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dd118ebc",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Discrete Lagrangian stuff\n",
    "function L(q,q̇)\n",
    "    0.5*m*q̇'*q̇ - m*g*q[2]\n",
    "end\n",
    "\n",
    "function Ld(q1,q2)\n",
    "    qm = 0.5*(q1 + q2)\n",
    "    vm = (q2-q1)/h\n",
    "    return L(qm,vm)\n",
    "end\n",
    "\n",
    "function D1Ld(q1,q2)\n",
    "    return ForwardDiff.gradient(dq1->Ld(dq1,q2),q1)\n",
    "end\n",
    "\n",
    "function D2Ld(q1,q2)\n",
    "    return ForwardDiff.gradient(dq2->Ld(q1,dq2),q2)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "80f78ea4",
   "metadata": {},
   "outputs": [],
   "source": [
    "function DEL(q1,q2,q3,n)\n",
    "    D2Ld(q1,q2) + D1Ld(q2,q3) + h*Dϕ'*n\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "068cdd22",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Boilerplate setup code to interface with IPOPT.\n",
    "\n",
    "struct ProblemMOI <: MOI.AbstractNLPEvaluator\n",
    "    n_nlp::Int\n",
    "    m_nlp::Int\n",
    "    idx_ineq\n",
    "    obj_grad::Bool\n",
    "    con_jac::Bool\n",
    "    sparsity_jac\n",
    "    sparsity_hess\n",
    "    primal_bounds\n",
    "    constraint_bounds\n",
    "    hessian_lagrangian::Bool\n",
    "end\n",
    "\n",
    "function ProblemMOI(n_nlp,m_nlp;\n",
    "        idx_ineq=(1:0),\n",
    "        obj_grad=true,\n",
    "        con_jac=true,\n",
    "        sparsity_jac=sparsity_jacobian(n_nlp,m_nlp),\n",
    "        sparsity_hess=sparsity_hessian(n_nlp,m_nlp),\n",
    "        primal_bounds=primal_bounds(n_nlp),\n",
    "        constraint_bounds=constraint_bounds(m_nlp,idx_ineq=idx_ineq),\n",
    "        hessian_lagrangian=false)\n",
    "\n",
    "    ProblemMOI(n_nlp,m_nlp,\n",
    "        idx_ineq,\n",
    "        obj_grad,\n",
    "        con_jac,\n",
    "        sparsity_jac,\n",
    "        sparsity_hess,\n",
    "        primal_bounds,\n",
    "        constraint_bounds,\n",
    "        hessian_lagrangian)\n",
    "end\n",
    "\n",
    "function constraint_bounds(m; idx_ineq=(1:0))\n",
    "    c_l = zeros(m)\n",
    "\n",
    "    c_u = zeros(m)\n",
    "    c_u[idx_ineq] .= Inf\n",
    "    \n",
    "    return c_l, c_u\n",
    "end\n",
    "\n",
    "function row_col!(row,col,r,c)\n",
    "    for cc in c\n",
    "        for rr in r\n",
    "            push!(row,convert(Int,rr))\n",
    "            push!(col,convert(Int,cc))\n",
    "        end\n",
    "    end\n",
    "    return row, col\n",
    "end\n",
    "\n",
    "function sparsity_jacobian(n,m)\n",
    "\n",
    "    row = []\n",
    "    col = []\n",
    "\n",
    "    r = 1:m\n",
    "    c = 1:n\n",
    "\n",
    "    row_col!(row,col,r,c)\n",
    "\n",
    "    return collect(zip(row,col))\n",
    "end\n",
    "\n",
    "function sparsity_hessian(n,m)\n",
    "\n",
    "    row = []\n",
    "    col = []\n",
    "\n",
    "    r = 1:m\n",
    "    c = 1:n\n",
    "\n",
    "    row_col!(row,col,r,c)\n",
    "\n",
    "    return collect(zip(row,col))\n",
    "end\n",
    "\n",
    "function MOI.eval_objective(prob::MOI.AbstractNLPEvaluator, x)\n",
    "    objective(x)\n",
    "end\n",
    "\n",
    "function MOI.eval_objective_gradient(prob::MOI.AbstractNLPEvaluator, grad_f, x)\n",
    "    ForwardDiff.gradient!(grad_f,objective,x)\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "function MOI.eval_constraint(prob::MOI.AbstractNLPEvaluator,g,x)\n",
    "    constraint!(g,x)\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "function MOI.eval_constraint_jacobian(prob::MOI.AbstractNLPEvaluator, jac, x)\n",
    "    ForwardDiff.jacobian!(reshape(jac,prob.m_nlp,prob.n_nlp), constraint!, zeros(prob.m_nlp), x)\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "function MOI.features_available(prob::MOI.AbstractNLPEvaluator)\n",
    "    return [:Grad, :Jac]\n",
    "end\n",
    "\n",
    "MOI.initialize(prob::MOI.AbstractNLPEvaluator, features) = nothing\n",
    "MOI.jacobian_structure(prob::MOI.AbstractNLPEvaluator) = prob.sparsity_jac\n",
    "\n",
    "function ipopt_solve(x0,prob::MOI.AbstractNLPEvaluator;\n",
    "        tol=1.0e-6,c_tol=1.0e-6,max_iter=10000)\n",
    "    x_l, x_u = prob.primal_bounds\n",
    "    c_l, c_u = prob.constraint_bounds\n",
    "\n",
    "    nlp_bounds = MOI.NLPBoundsPair.(c_l,c_u)\n",
    "    block_data = MOI.NLPBlockData(nlp_bounds,prob,true)\n",
    "\n",
    "    solver = Ipopt.Optimizer()\n",
    "    solver.options[\"max_iter\"] = max_iter\n",
    "    solver.options[\"tol\"] = tol\n",
    "    solver.options[\"constr_viol_tol\"] = c_tol\n",
    "    \n",
    "    #Uncomment the following line to turn off verbose IPOPT output\n",
    "    solver.options[\"print_level\"] = 0\n",
    "\n",
    "    x = MOI.add_variables(solver,prob.n_nlp)\n",
    "\n",
    "    for i = 1:prob.n_nlp\n",
    "        MOI.add_constraint(solver, x[i], MOI.LessThan(x_u[i]))\n",
    "        MOI.add_constraint(solver, x[i], MOI.GreaterThan(x_l[i]))\n",
    "        MOI.set(solver, MOI.VariablePrimalStart(), x[i], x0[i])\n",
    "    end\n",
    "\n",
    "    # Solve the problem\n",
    "    MOI.set(solver, MOI.NLPBlock(), block_data)\n",
    "    MOI.set(solver, MOI.ObjectiveSense(), MOI.MIN_SENSE)\n",
    "    MOI.optimize!(solver)\n",
    "\n",
    "    # Get the solution\n",
    "    res = MOI.get(solver, MOI.VariablePrimal(), x)\n",
    "\n",
    "    return res\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ab76414b",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Objective and constraint functions for IPOPT\n",
    "\n",
    "function objective(z)\n",
    "    qn = z[1:2]\n",
    "    n = z[3]\n",
    "    s = z[4]\n",
    "    \n",
    "    return s #Minimize slacks associated with relaxed complementarity condition\n",
    "end\n",
    "\n",
    "function constraint!(c,z)\n",
    "    qn = z[1:2]\n",
    "    n = z[3]\n",
    "    s = z[4]\n",
    "    \n",
    "    c .= [DEL(qhist[:,k-1],qhist[:,k],qn,n); #DEL\n",
    "          ϕ(qn); #signed distance\n",
    "          s-n*ϕ(qn)] #relaxed complementarity\n",
    "    \n",
    "    return nothing\n",
    "end\n",
    "\n",
    "#Specify the indecies of c (constraint output) that should be non-negative.\n",
    "#The rest will be treated as equality constraints.\n",
    "nonnegative_constraint_indices = (3:4)\n",
    "\n",
    "function primal_bounds(n)\n",
    "    #Enforce simple bound constraints on the decision variables (e.g. positivity) here\n",
    "    \n",
    "    x_l = [-Inf*ones(2); zeros(2)]\n",
    "    x_u = Inf*ones(4)\n",
    "    \n",
    "    return x_l, x_u\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d17b6565",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Solve with IPOPT\n",
    "n_nlp = 4\n",
    "m_nlp = 4\n",
    "nlp_prob = ProblemMOI(n_nlp,m_nlp, idx_ineq=nonnegative_constraint_indices);\n",
    "\n",
    "#Initial conditions\n",
    "qhist = zeros(2,N)\n",
    "qhist[:,1] .= q0\n",
    "qhist[:,2] .= q1\n",
    "\n",
    "nhist = zeros(N-1)\n",
    "shist = zeros(N-1)\n",
    "k = 0\n",
    "\n",
    "for kk = 2:(N-1)\n",
    "    k = kk\n",
    "    z_guess = [qhist[:,k]; 0; 1.0]\n",
    "    z_sol = ipopt_solve(z_guess,nlp_prob);\n",
    "    qhist[:,k+1] .= z_sol[1:2]\n",
    "    nhist[k] = z_sol[3]\n",
    "    shist[k] = z_sol[4]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ee019863",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist,qhist[1,:])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e84d4f14",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist,qhist[2,:])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fb550522",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist[1:end-1],nhist)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "27aad2a2",
   "metadata": {},
   "outputs": [],
   "source": [
    "maximum(shist)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "19964753",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Parameters\n",
    "g = 9.81\n",
    "m = 1.0\n",
    "μ = .05\n",
    "h = 0.05 #10 Hz\n",
    "Tf = 1.5 #final time (sec)\n",
    "thist = Array(0:h:Tf)\n",
    "N = length(thist)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cfde5c20",
   "metadata": {},
   "outputs": [],
   "source": [
    "function DEL(q1,q2,q3,n,b)\n",
    "    D2Ld(q1,q2) + D1Ld(q2,q3) + h*[b;n]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "57407878",
   "metadata": {},
   "outputs": [],
   "source": [
    "function smoothsqrt(x)\n",
    "    ϵ = 1e-6\n",
    "    return sqrt(x+ϵ*ϵ) - ϵ\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c04c5f1a",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Objective and constraint functions for IPOPT with Friction\n",
    "\n",
    "function objective(z)\n",
    "    qn = z[1:2]\n",
    "    n = z[3]\n",
    "    b = z[4]\n",
    "    λ = z[5]\n",
    "    s = z[6:7]\n",
    "    \n",
    "    return sum(s) #Minimize slacks associated with relaxed complementarity conditions\n",
    "end\n",
    "\n",
    "function constraint!(c,z)\n",
    "    qn = z[1:2]\n",
    "    n = z[3]\n",
    "    b = z[4]\n",
    "    λ = z[5]\n",
    "    s = z[6:7]\n",
    "    \n",
    "    vm = (qn-qhist[:,k])/h\n",
    "    \n",
    "    c .= [DEL(qhist[:,k-1],qhist[:,k],qn,n,b); #DEL\n",
    "          [1; 0]'*vm + λ*b/smoothsqrt(b*b); #maximum dissipation\n",
    "          μ*n - smoothsqrt(b*b); #friction cone\n",
    "          s[1]-n*ϕ(qn); #relaxed complementarity\n",
    "          s[2]-λ*(μ*n - smoothsqrt(b*b))] #relaxed complementarity\n",
    "    \n",
    "    return nothing\n",
    "end\n",
    "\n",
    "#Specify the indecies of c (constraint output) that should be non-negative.\n",
    "#The rest will be treated as equality constraints.\n",
    "nonnegative_constraint_indices = (4:6)\n",
    "\n",
    "function primal_bounds(n)\n",
    "    #Enforce simple bound constraints on the decision variables (e.g. positivity) here\n",
    "    \n",
    "    x_l = [-Inf; 0; 0; -Inf; zeros(3)]\n",
    "    x_u = Inf*ones(7)\n",
    "    \n",
    "    return x_l, x_u\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f758a27e",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Solve with IPOPT\n",
    "n_nlp = 7\n",
    "m_nlp = 6\n",
    "nlp_prob = ProblemMOI(n_nlp,m_nlp, idx_ineq=nonnegative_constraint_indices);\n",
    "\n",
    "#Initial conditions\n",
    "qhist = zeros(2,N)\n",
    "qhist[:,1] .= q0\n",
    "qhist[:,2] .= q1\n",
    "\n",
    "nhist = zeros(N-1)\n",
    "bhist = zeros(N-1)\n",
    "λhist = zeros(N-1)\n",
    "shist = zeros(2,N-1)\n",
    "k = 0\n",
    "\n",
    "for kk = 2:(N-1)\n",
    "    k = kk\n",
    "    z_guess = [qhist[:,k]; ones(5)]\n",
    "    z_sol = ipopt_solve(z_guess,nlp_prob);\n",
    "    qhist[:,k+1] .= z_sol[1:2]\n",
    "    nhist[k] = z_sol[3]\n",
    "    bhist[k] = z_sol[4]\n",
    "    λhist[k] = z_sol[5]\n",
    "    shist[:,k] .= z_sol[6:7]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "57d88df0",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist, qhist[1,:])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "47ddfdf8",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist, qhist[2,:])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6a4ca995",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist[1:end-1], nhist)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "05fec2b1",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist[1:end-1], bhist)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "355b4bf9",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(thist[1:end-1], λhist)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "221e36fb",
   "metadata": {},
   "outputs": [],
   "source": [
    "maximum(abs.(shist))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b730e642",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.6.7",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
